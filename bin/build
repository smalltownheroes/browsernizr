#!/usr/bin/env coffee

path = require 'path'
fs = require 'fs'

require 'shelljs/global'

unAMD = (src, reqPath='./') ->
  requires = src.match /define\((\[.*\])[ ,\)]+function[ ]*\((.*)\)/
  exports = src.match /return\s+([^;]*)[;\s]*?\}\);\s*?$/
  src = src.replace /define\(.*?\{/, ''
  src = src.replace /(return\s+.*\s*?\}\);\s*?$|\}\);\s*?$)/, ''
  if requires?
    modules = eval requires[1]
    names = requires[2].split(',').map (s) -> s.trim()
    req = ''
    for i in [0...modules.length]
      req += "var #{ names[i] } = " if names[i]?.length > 0
      req += "require('#{ reqPath }#{ modules[i] }');\n"
    #console.log req
    src = "#{ req }\n#{ src }\n"
  if exports?
    src += "\nmodule.exports = #{ exports[1] };"
  return src

findLibFiles = (location) ->
  find(location).filter (file) -> 
    !fs.statSync(file).isDirectory() and file.indexOf('/') is -1

findTestFiles = (location) ->
  find(location).filter (file) -> 
    !fs.statSync(file).isDirectory()


work_dir = path.resolve __dirname, './../'
modernizr_dir = path.join work_dir, './node_modules/modernizr'
lib_dir = path.join work_dir, './lib'
tests_dir = path.join work_dir, './lib/test'

# cleanup
rm '-rf', "#{ lib_dir }/*"
rm '-rf', "#{ tests_dir }/*"

# copy modernizr source
cd modernizr_dir
cp '-r', './src/*', "#{ lib_dir }/"
cp '-r', './feature-detects/*', "#{ tests_dir }/"

# replace asynchronous defines with require and exports
cd lib_dir
for file in findLibFiles '.'
  #console.log file
  src = cat file
  unAMD(src).to file



cd tests_dir
for file in findTestFiles '.'
  src = cat file
  depth = file.split('/').length
  libPath = './'
  for [0...depth]
    libPath += '../'
  
  #console.log libPath, file
  #unless libPath.indexOf('test') > -1
  #libPath += 'lib/'


  unAMD(src, libPath).to file



